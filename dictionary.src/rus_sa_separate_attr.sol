// -----------------------------------------------------------------

// Эксперимент - попробуем считать группы прилагательных, оторванных
// от своих существительных, особым актором.

patterns ОбособленныйАтрибут export { node:root_node РОД ЧИСЛО }

wordentry_set ВопроситПрил=прилагательное:{ какой }

// какой отсюда следует вывод?
// ^^^^^
pattern ОбособленныйАтрибут
{
 ГруппаПрил1{  ВопроситПрил ПАДЕЖ:ИМ } : export { node:root_node РОД ЧИСЛО }
}
: ngrams { -2 }


// Тут зверь появился, высок и космат, и так же, как многие прочие звери, узнав человека, отпрянул назад.
//                   ^^^^^^^^^^^^^^^^^
pattern ОбособленныйАтрибут
{
 comma1=','
 a=КраткПрил2 : export { node:root_node РОД ЧИСЛО }
 @noshift(ПравыйОграничительОборота)
 comma2=@coalesce(',')
}
: links
{
 a.{
    <PUNCTUATION>comma1
    ~<PUNCTUATION>comma2
   }
}
: ngrams { -13 }


// помощь мне твоя нужна
//            ^^^^
pattern ОбособленныйАтрибут
{
 ГруппаПрил2{ ПАДЕЖ:ИМ } : export { node:root_node РОД ЧИСЛО }
}
: ngrams { -10 }


// Безмолвен стоял лес.
// ^^^^^^^^^
pattern ОбособленныйАтрибут
{
 КраткПрил2 : export { node:root_node РОД ЧИСЛО }
}
: ngrams { -12 }


// Несколько обезьян погибли, зарубленные топорами
//                          ^^^^^^^^^^^^^^^^^^^^^^
pattern ОбособленныйАтрибут
{
 comma1=','
 a=ПричОборот2{ ПАДЕЖ:ИМ } : export { node:root_node РОД ЧИСЛО }
 @noshift(ПравыйОграничительОборота)
 comma2=@coalesce(',')
}
: links
{
 a.{
    <PUNCTUATION>comma1
    ~<PUNCTUATION>comma2
   }
}
: ngrams { -8 }

// -----------------------------------------------------------------
