// Правила оценки достоверности конструкций с существительным в роли
// главного слова.

// кому из нас нужна твоя помощь?
// ^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { местоим_сущ:кто{}.предлог:из{} }
  then 2
}


// Студент с марта не ходит на лекции
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{}.предлог:*{}.НазваниеМесяца }
  then -20
}

// Чтобы уменьшить вероятность связывания:
// Сделайте вид, что вы не ходячие.
//          ^^^^^^^^
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{}.<RIGHT_LOGIC_ITEM>','.местоим_сущ:что{} }
  then -10
}



// ----------------------------------------------------------
/*
// (???? проверить на корпусе ???)
// Немного подавим одновременное присоединение прилагательного ЕГО/ЕЕ/ИХ и
// правого родительного дополнения:
// Я отдаю в его распоряжение своих рыцарей
//           ^^^ ^^^^^^^^^^^^ ^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{}.{ <ATTRIBUTE>прилагательное:*{ ЕгоЕеИх } <RIGHT_GENITIVE_OBJECT>*:*{} } }
  then -1
}
*/
// ----------------------------------------------------------


tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{ одуш:одуш }.предлог:с{}.ЗапретСвязывания_С }
  then -20
}

// предлог БЕЗ антонимичен предлогу С, поэтому фильтруем аналогично.
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{ одуш:одуш }.предлог:без{}.ЗапретСвязывания_С }
  then -20
}



// Для ФИО и топонимов подавляем прикрепление любых предлогов,
// кроме небольшого списка разрешенных:
wordentry_set РазрешенныеПредлогиДляФИО=предлог:{
С,
В,
НА,
У,
ИЗ // Сергей из нашего дома исчез
}

tree_scorers ЗапрещенноеПредлДопДляСущ

tree_scorer ЗапрещенноеПредлДопДляСущ language=Russian
{
 if context { предлог:*{ ~РазрешенныеПредлогиДляФИО } }
  then 1
}

tree_scorer ЗапрещенноеПредлДопДляСущ language=Russian
{
 if context { предлог:в{}.*:*{ падеж:вин } }
  then 1
}

tree_scorer ЗапрещенноеПредлДопДляСущ language=Russian
{
 if context { предлог:на{}.*:*{ падеж:вин } }
  then 1
}

tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{ одуш:одуш <в_класс>существительное:ИМЯ{} }.ЗапрещенноеПредлДопДляСущ }
  then -100
}

tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{ одуш:одуш <в_класс>существительное:ФАМИЛИЯ{} }.ЗапрещенноеПредлДопДляСущ }
  then -100
}

tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{ одуш:неодуш <в_класс>существительное:СТРАНА{} }.ЗапрещенноеПредлДопДляСущ }
  then -100
}

tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{ одуш:неодуш <в_класс>существительное:ГОРОД{} }.ЗапрещенноеПредлДопДляСущ }
  then -100
}



// Белоруссия с марта повышает экспортные пошлины на нефть
// ^^^^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{ одуш:неодуш <в_класс>существительное:СТРАНА{} }.предлог:с{}.*:*{ падеж:род } }
  then -100
}

tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{ одуш:неодуш <в_класс>существительное:ГОРОД{} }.предлог:с{}.*:*{ падеж:род } }
  then -100
}


// Данные о промпроизводстве США за ноябрь оказались очень хорошими
wordentry_set ЗапретПредлогДляТопоним=предлог:{ за, до, у, для, "из-за" }

tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{ одуш:неодуш <в_класс>существительное:СТРАНА{} }.ЗапретПредлогДляТопоним }
  then -100
}

tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{ одуш:неодуш <в_класс>существительное:ГОРОД{} }.ЗапретПредлогДляТопоним }
  then -100
}

tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{ одуш:неодуш <в_класс>существительное:ОСТРОВ{} }.ЗапретПредлогДляТопоним }
  then -100
}

// Подавляем присоединение предложных паттернов с семантикой обстоятельства времени
// к одушевленным существительным:
wordentry_set ЗапретПредлогДляОдуш=предлог:{ за, до, к, после, перед, в, с, "в течение" }

tree_scorers Темпораним

tree_scorer Темпораним language=Russian
{
 if context { ДеньНедели }
  then 1
}

tree_scorer Темпораним language=Russian
{
 if context { НазваниеМесяца }
  then 1
}

tree_scorer Темпораним language=Russian
{
 if context { ИнтервалВремени }
  then 1
}

tree_scorer Темпораним language=Russian
{
 if context { СущСоЗначВрем }
  then 1
}

// Животные за март увеличили удойность вдвое
// ^^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{ одуш:одуш }.ЗапретПредлогДляТопоним.Темпораним }
  then -2
}



// она поедет на встречу с заказчиком
//               ^^^^^^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian 
{
 if context { существительное:встреча{}.предлог:с{}.ОдушОбъект }
  then 1
}

// Он оставил заметный след в науке.
//                     ^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian 
{
 if context { существительное:след{}.предлог:в{}.НеодушОбъект{ падеж:предл } }
  then 1
}

// простой судов вследствие сбоев в снабжении
// ^^^^^^^^      ^^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian 
{
 if context { существительное:простой{}.предлог:вследствие{}.НеодушОбъект{ падеж:род } }
  then 1
}


// простой судов вследствие сбоев в снабжении
//                          ^^^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian 
{
 if context { существительное:сбой{}.предлог:в{}.НеодушОбъект{ падеж:предл } }
  then 1
}


// Подавляем правое родительное дополнение для существительного ЛЕТО:
// за последние сто лет многих удалось вывести оттуда
//                  ^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian 
{
 if context { существительное:лето{}.<RIGHT_GENITIVE_OBJECT>*:*{ падеж:род } }
  then -5
}


// Жителю Италии трудно обойтись без макарон.
// ^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:житель{}.существительное:*{ <в_класс>существительное:страна{} падеж:род } }
  then 2
}

tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:гражданин{}.существительное:*{ <в_класс>существительное:страна{} падеж:род } }
  then 2
}


// Дания отправит транспортный самолет в Мали для помощи французским войскам
//                                                ^^^^^^^^^^^^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:помощь{}.существительное:*{ падеж:дат } }
  then 1
}


// вдруг по толпе зрителей пробежала волна криков
//          ^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:толпа{}.существительное:*{ число:мн падеж:род одуш:одуш } }
  then 2
}


// Глава Италии рассчитывает на скорое формирование коалиционного правительства
// ^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:глава{ одуш:одуш }.<RIGHT_GENITIVE_OBJECT>существительное:*{ одуш:неодуш <в_класс>существительное:страна{} падеж:род } }
  then 2
}


// осмотр комнаты занял двадцать минут.
// ^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:осмотр{}.<RIGHT_GENITIVE_OBJECT>существительное:*{ падеж:род } }
  then 2
}



// Президент Франции пообещал облегчить визовый режим с Россией
// ^^^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:президент{}.<RIGHT_GENITIVE_OBJECT>существительное:*{ <в_класс>существительное:страна{} } }
  then 10
}



// ветер принес запах дыма.
//              ^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:запах{}.*:*{ падеж:род } }
  then 1
}


// Соединенные Штаты признали правительство Сомали
//                            ^^^^^^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:правительство{}.<RIGHT_GENITIVE_OBJECT>существительное:*{ падеж:род <в_класс>существительное:страна{} } }
  then 1
}


// Оля сделала глоток кофе.
//             ^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:глоток{}.<RIGHT_GENITIVE_OBJECT>существительное:*{ одуш:неодуш падеж:род <в_класс>существительное:напиток{} } }
  then 2
}


// пленник оставил попытки спастись.
//                 ^^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:попытка{}.инфинитив:*{} }
  then 1
}



// ужас наполнил душу странника.
//               ^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:душа{}.существительное:*{ одуш:одуш } }
  then 2
}


// Николай, лесник предупредил браконьеров о серьезных последствиях?
//                             ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{ одуш:одуш }.предлог:о{}.*{} }
  then -1
}


// Учителя взывают к совести хулигана
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:совесть{}.<RIGHT_GENITIVE_OBJECT>существительное:*{ одуш:одуш } }
  then 1
}


// Он сделал важное открытие в области физики.
//                  ^^^^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:открытие{}.'в области'.существительное:*{ одуш:неодуш } }
  then 1
}


// Он занял первое место в соревнованиях по плаванию.
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:соревнование{}.'по'.существительное:*{ одуш:неодуш } }
  then 1
}


// Она решила задачи по математике.
//            ^^^^^^^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:задача{}.'по'.существительное:*{ одуш:неодуш <в_класс>существительное:наука{} } }
  then 1
}



// Я получил счёт за телефон.
//           ^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:счет{}.'за'.существительное:*{ одуш:неодуш } }
  then 1
}



// Этот карандаш хорошо пишет.
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{ род:муж падеж:им }.'этот'{ class:прилагательное падеж:им } }
  then 2
}

// Этот текст правил редактор.
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{ род:муж падеж:вин }.'этот'{ class:прилагательное падеж:вин } }
  then 2
}


// Сравнение чисел с плавающей точкой
//           ^^^^^^^^^^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:число{}.предлог:с{}.'точкой'.'плавающей' }
  then 2
}



// президент Соединенных Штатов Америки
//           ^^^^^^^^^^^^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:штат{ число:мн }.{ прилагательное:соединенный{ число:мн } существительное:Америка{ падеж:род число:ед } } }
  then 2
}



// стул господина премьер-министра Зимбабве
//                ^^^^^^^^^^^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:премьер-министр{}.<RIGHT_NAME>существительное:*{ <в_класс>существительное:страна{} } }
  then -1
}


// я хочу купить или арендовать домик у озера
//                              ^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:домик{}.предлог:у{}.существительное:*{ одуш:неодуш } }
  then 1
}



// целовать Петю между глаз
//          ^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{ одуш:одуш }.предлог:между{} }
  then -2
}


// характерные черты лица
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:черта{}.существительное:лицо{ одуш:неодуш } }
  then 1
}


// Увидев моё лицо, он рассердился.
//        ^^^^^^^^
wordentry_set АтрибутДляЛицНеодуш=прилагательное:{
 мой, твой, наш, ваш, свой, "чей-то", красивый, умный, недовольный, незнакомый,
 знакомый, чужой
}
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:лицо{ одуш:неодуш }.АтрибутДляЛицНеодуш }
  then 2
}





// сведения от посторонних лиц
//             ^^^^^^^^^^^^^^^
wordentry_set АтрибутДляЛицОдуш=прилагательное:{
 посторонний, неизвестный, некий,
 третий // Наш спор решат третьи лица.
}
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:лицо{ одуш:одуш }.АтрибутДляЛицОдуш }
  then 2
}



// злое выражение лица
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:выражение{}.'лица'{ одуш:неодуш } }
  then 1
}


// я сегодня вечером буду есть салат из свежих овощей
//                             ^^^^^           ^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:салат{}.предлог:из{}.существительное:*{ <в_класс>существительное:овощ{} } }
  then 1
}


// Женя только начала есть суп из брокколи
//                         ^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:суп{}.предлог:из{}.существительное:*{ <в_класс>существительное:овощ{} } }
  then 1
}




// Лица прохожих были зелены.
// ^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:лицо{ одуш:неодуш }.существительное:*{} }
  then 1
}


// доверенное лицо
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:лицо{ одуш:одуш }.прилагательное:доверенный{} }
  then 10
}


// Он живёт в квартире с ванной.
//            ^^^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:квартира{}.предлог:с{}.существительное:*{ одуш:неодуш } }
  then 1
}


// Я купил избранные сочинения Пушкина в одном томе.
//                   ^^^^^^^^^         ^       ^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:сочинение{}.предлог:в{}.существительное:том{ падеж:предл } }
  then 1
}




// Пожалуйста, дайте мне подумать над этой задачей.
//                                    ^^^^ ^^^^^^^
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{ 2 род:жен число:ед падеж:твор }.'этой'{1} }
  then 1
}

/*
// Это предприятие перешло в другие руки.
// ^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{ 2 род:ср число:ед падеж:им }.прилагательное:этот{ 1 число:ед род:ср падеж:им } }
  then 2
}

tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{ 2 род:ср число:ед падеж:вин }.прилагательное:этот{ 1 число:ед род:ср падеж:вин } }
  then 2
}
*/

// Он долго смотрел на эту картину.
//                     ^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{ 2 падеж:вин род:жен }.'эту'{ 1 class:прилагательное } }
  then 1
}


// Самолёт взял направление на юг.
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:направление{}.предлог:на{}.СторонаСвета{ падеж:вин } }
  then 1
}





// Мы купили билеты на оперу.
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:билет{}.предлог:на{}.существительное:*{ одуш:неодуш } }
  then 1
}



// Ты сделал четыре ошибки в диктанте.
//           ^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{ род:жен }.числительное:четыре{ род:жен } }
  then 1
}


tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{ род:ср }.числительное:четыре{ род:ср } }
  then 1
}

tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{ род:муж }.числительное:четыре{ род:муж } }
  then 1
}



// Сохраните это на память обо мне.
//                  ^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:память{}.'обо'.местоимение:я{ падеж:предл } }
  then 2
}



// Мишка взял бутылку с подсолнечным маслом.
//            ^^^^^^^ ^              ^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:бутылка{}.предлог:с{}.существительное:*{ одуш:неодуш } }
  then 2
}


// я взял один кофе и один бутерброд с сыром
//                         ^^^^^^^^^ ^ ^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:бутерброд{}.предлог:с{}.существительное:*{ одуш:неодуш  } }
  then 2
}


// Связь с городом не работала уже несколько дней.
// ^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:связь{}.предлог:с{} }
  then 1
}

// скорей бы начать работу над этим проектом
//                             ^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{ падеж:твор 2 }.'этим'{ 1 class:прилагательное падеж:твор } }
  then 2
}

// К этому человеку надо присмотреться
//   ^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{ падеж:дат число:ед 2 }.'этому'{ 1 class:прилагательное падеж:дат число:ед } }
  then 2
}


// Она заявила свои права на наследство.
//                  ^^^^^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:право{}.предлог:на{}.существительное:*{ падеж:вин } }
  then 1
}


// Начало прошлого года
//        ^^^^^^^^^^^^^
wordentry_set МоментВремениМужРод=существительное:{ год, месяц, день, час, вечер }
tree_scorer ВалентностьСущ language=Russian
{
 if context { 'прошлого'{ class:существительное }.<RIGHT_GENITIVE_OBJECT>МоментВремениМужРод  }
  then -1
}

// о новой и интересной работе над вакциной против гриппа
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:вакцина{}.предлог:против{}.существительное:*{} }
  then 2
}



// сельдь под шубой
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:сельдь{}.'под'.'шубой' }
  then 1
}


// Мы боремся все доступными средствами с ошибками в программе
//                                      ^^^^^^^^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:ошибка{}.'в'.существительное:программа{падеж:предл} }
  then 1
}


// передача врагу секретной информации
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:передача{}.существительное:*{падеж:род} }
  then 1
}

// Профессора встретили группу студентов университета дружбы народов, изучающих физику
//                             ^^^^^^^^^                              ^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:студент{}.','.прилагательное:изучающий{} }
  then 1
}



// группа студентов университета дружбы народов, получивших повышенные стипендии
//        ^^^^^^^^^                              ^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:студент{}.','.прилагательное:получивший{} }
  then 1
}




// стипендия студента университета дружбы народов, живших в союзных республиках бывшего СССР
//                                        ^^^^^^^  ^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:народ{}.','.прилагательное:живший{} }
  then 1
}


tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:плов{}.предлог:с{}.существительное:баранина{} }
  then 1
}




// кофе со сливками
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:кофе{}.предлог:с{}.существительное:сливки{падеж:твор} }
  then 1
}



// Он оставил заметный след в науке.
//                     ^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:след{}.предлог:в{}.*:*{ падеж:предл } }
  then 2
}


// Эти деньги можно употребить на ремонт фабрики.
//                                ^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:ремонт{}.<RIGHT_GENITIVE_OBJECT>НеодушОбъект{ ПАДЕЖ:РОД } }
  then 1
}


tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:максимум{}.существительное:*{ падеж:род } }
  then 1
}

// Часть + чего-то:
// дадут часть силы добычи.
//       ^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:часть{}.*:*{ падеж:род } }
  then 1
}


// Два человека говорят о трех кенгуру
//                      ^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{ падеж:предл }.числительное:три{ падеж:предл } }
  then 1
}

// трём котам
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{ падеж:дат }.числительное:три{ падеж:дат } }
  then 1
}


// пять котов
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{ падеж:род }.числительное:пять{} }
  then 1
}

// после начала битвы прошли считанные секунды.
//       ^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:начало{}.существительное:*{ падеж:род одуш:неодуш } }
  then 2
}


// право именоваться лидером в области данной технологии
//                   ^^^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:лидер{}.предлог:в области{} }
  then 1
}


// Для ОДИН предпочитаем прилагательное, а не числительное:
// у тебя был только один мужчина?
//                   ^^^^
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{}.прилагательное:один{} }
  then 1
}

// какой образ вас интересует?
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{}.<ATTRIBUTE>прилагательное:какой{} }
  then 2
}



// мой взгляд нашел остаток твари.
//                  ^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:остаток{}.существительное:*{ падеж:род } }
  then 2
}


// Снимаем омонимию ОДУШ-НЕОДУШ:
// лицо у Андрея изменилось.
// ^^^^ ^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:лицо{ одуш:неодуш }.предлог:у{} }
  then 1
}


// Для одушевленных сущ. присоединение предложного дополнения с ОТ маловероятно:
// Степа от неожиданности вздрогнул.
// ~~~~~ ^^
tree_scorer ВалентностьСущ language=Russian generic
{ if context { существительное:*{ одуш:одуш }.предлог:от{} } then -10 }



// Предлог БЕЗ редко присоединяется к одушевленным, особенно к именам:
// Альберт без особого удивления еще раз ощупал свое тело.
// ~~~~~~~~~~~
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{ одуш:одуш <в_класс>существительное:имя{} }.предлог:без{} }
  then -10
}



// Антонио ни о ком не слышал
// ~~~~~~~ ^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:*{ одуш:одуш }.местоим_сущ:никто{} }
  then -10
}


// все путешествие заняло полтора часа
// ^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian generic
{ if context { существительное:*{ 2 одуш:неодуш род:ср число:ед }.<ATTRIBUTE>прилагательное:весь{ 1 род:ср число:ед } } then 1 }





// Сейчас ему грозит до семи лет лишения свободы
//                      ^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:лето{ падеж:род число:мн }.числительное:*{} }
  then 5
}


// листья капусты способствуют выведению из организма холестерина
//                             ^^^^^^^^^              ^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{ if context { существительное:выведение{}.*:*{ падеж:род } } then 2 }



// Выделяют периодические и непериодические изменения погоды.
//                                         ^^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{ if context { существительное:изменение{}.*:*{ падеж:род } } then 2 }



// приблизительно первая четверть жизни прошла.
//                       ^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{ if context { существительное:четверть{}.*:*{ падеж:род } } then 2 }


// Половина лета прошла.
// ^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{ if context { существительное:половина{}.*:*{ падеж:род } } then 2 }



// у тебя был только один мужчина?
//                   ^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{ if context { существительное:*:*{}.прилагательное:один{} } then 1 }



// мимо нас пробежала группа стражников.
//                    ^^^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{ if context { существительное:группа{}.*:*{ падеж:род } } then 2 }

// ----------------------------------------------------------
// Подавим присоединение предложного дополнения с семантикой
// обстоятельства способа:

// ++++++

// Кто-то с сомнением хмыкнул.
// ^^^^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { местоим_сущ:*{}.предлог:с{}.Сущ_ДляОбст_С }
  then -5
}

// Я с сомнением хмыкнул.
// ^^^^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { местоимение:*{}.предлог:с{}.Сущ_ДляОбст_С }
  then -5
}


// Миша с сомнением хмыкнул.
// ^^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{ одуш:одуш }.предлог:с{}.Сущ_ДляОбст_С }
  then -5
}

// Колесо с грохотом отвалилось
// ^^^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{ одуш:неодуш }.предлог:с{}.Сущ_ДляОбст_С }
  then -3
}

// ++++

// Кто-то от ужаса ойкнул
// ^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { местоим_сущ:*{}.предлог:от{}.Сущ_ДляОбст_ОТ }
  then -5
}

// Я от ужаса ойкнул
// ^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { местоимение:*{}.предлог:от{}.Сущ_ДляОбст_ОТ }
  then -5
}


// Миша от ужаса ойкнул
// ^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{ одуш:одуш }.предлог:от{}.Сущ_ДляОбст_ОТ }
  then -5
}

// Доска от давления хрустнула
// ^^^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{ одуш:неодуш }.предлог:от{}.Сущ_ДляОбст_ОТ }
  then -3
}

// +++++


// Кто-то без колебаний завопил
// ^^^^^^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { местоим_сущ:*{}.предлог:без{}.Сущ_ДляОбст_БЕЗ }
  then -5
}

// Я без колебаний сел
// ^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { местоимение:*{}.предлог:без{}.Сущ_ДляОбст_БЕЗ }
  then -5
}


// Миша без колебаний ойкнул
// ^^^^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{ одуш:одуш }.предлог:без{}.Сущ_ДляОбст_БЕЗ }
  then -5
}

// Колесо без задержки закрутилось
// ^^^^^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{ одуш:неодуш }.предлог:без{}.Сущ_ДляОбст_БЕЗ }
  then -3
}


// ++++

// Кто-то в ужасе закричал
// ^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { местоим_сущ:*{}.предлог:в{}.Сущ_ДляОбст_В }
  then -5
}

// Он в ужасе попятился
// ^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { местоимение:*{}.предлог:в{}.Сущ_ДляОбст_В }
  then -5
}


// Миша в ужасе попятился
// ^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{ одуш:одуш }.предлог:в{}.Сущ_ДляОбст_В }
  then -5
}

// Колонна в ужасе распалась
// ^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{ одуш:неодуш }.предлог:в{}.Сущ_ДляОбст_В }
  then -3
}


// -------------------------------------------

// поездка в Токио
tree_scorer ВалентностьСущ language=Russian
{ if context { существительное:поездка{}.предлог:в{}.ЛюбойТопонимНеизв{ падеж:вин } } then 2 }

// поездка в такси
tree_scorer ВалентностьСущ language=Russian
{ if context { существительное:поездка{}.предлог:в{}.ЛюбойТранспорт{ падеж:предл } } then 2 }

// -----------------------

// путешествие в Токио
tree_scorer ВалентностьСущ language=Russian
{ if context { существительное:путешествие{}.предлог:в{}.ЛюбойТопонимНеизв{ падеж:вин } } then 2 }

// -----------------------

// Выражение их лиц было трудно пнять
// ^^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:выражение{}.существительное:лицо{ одуш:одуш } }
  then -10
}

// -----------------------

tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:нацеленность{}.предлог:на{}.*:*{ одуш:неодуш падеж:вин } }
  then 2
}

// ------------------------

// отряд прошел по этому туннелю около мили
//                       ^^^^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{}.предлог:около{}.ЕдиницыДлины }
  then -5
}

// -----------------------------

// этот парень всех знает!
//      ~~~~~~ ^^^^
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { существительное:*{ одуш:одуш }.<RIGHT_GENITIVE_OBJECT>прилагательное:весь{ падеж:род } }
  then -5
}

// ++++++++++++++++++++++

// ДОСТИЖЕНИЕ (в значении ПОЛУЧЕНИЕ) прикрепляет родительное дополнение.
// Наиболее актуальной на сегодняшний день задачей премьер-министр считает скорейшее достижение двухпроцентного уровня инфляции
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:достижение{}.<RIGHT_GENITIVE_OBJECT>*:*{ падеж:род одуш:неодуш } }
  then 1
}


// +++++++++++++++++++++++++

// Глаза у отца сверкали гневом
// ^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:глаз{}.предлог:у{}.*:*{ одуш:одуш } }
  then 2
}

// ++++++++++++++++++++++++++

// лицо без гражданства
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:лицо{ одуш:одуш }.предлог:без{}.существительное:гражданство{ падеж:род } }
  then 2
}

// ++++++++++++++++++++++++++

// шла борьба за право нести великого князя
//               ^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:право{1}.инфинитив:*{2} }
  then 4
}

// -----------------------------------------

// у меня оставался только один шанс сохранить себе жизнь
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:шанс{1}.инфинитив:*{2} }
  then 4
}


// ---------------------------------------

// Поэтому для повышения устойчивости организма к простудным заболеваниям необходимо использовать препараты неспецифической профилактики
//             ^^^^^^^^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:повышение{}.<RIGHT_GENITIVE_OBJECT>*:*{ падеж:род одуш:неодуш } }
  then 5
}

tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:понижение{}.<RIGHT_GENITIVE_OBJECT>*:*{ падеж:род одуш:неодуш } }
  then 5
}

// --------------------------------------


// отпуск недостающих медикаментов из собственных коммерческих запасов
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:отпуск{}.{
                                        <RIGHT_GENITIVE_OBJECT>*:*{ падеж:род }
                                        предлог:из{}
                                       }
            }
  then 5
}

// ------------------------------------------------

// Дания поддерживает вступление Грузии в НАТО и признает ее территориальную целостность
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:вступление{}.предлог:в{}.*:*{ падеж:вин } }
  then 2
}

// ----------------------------------------------

// Обвинения против премьер-министра Пакистана
//                  ^^^^^^^^^^^^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:премьер-министр{}.<RIGHT_GENITIVE_OBJECT>существительное:*{ <в_класс>существительное:страна{} } }
  then 10
}

// -----------------------------------------------

// в распоряжении ВМС Индии в ближайшие годы останется 7-8 субмарин.
//                ^^^^^^^^^

tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:ВМС{}.<RIGHT_GENITIVE_OBJECT>существительное:*{ <в_класс>существительное:страна{} } }
  then 10
}

// -----------------------------------------------


// Избегаем соединения существительных с разной обушевленностью
//
// В ней приняли участие специалисты из России, Кореи, США, Канады, Швеции, Англии
//                       ^^^^^^^^^^^            ~~~~~  ~~~~
tree_scorer ВалентностьСущ language=Russian generic
{
 if context { n1=существительное:*{}.
               <RIGHT_LOGIC_ITEM>*:*{}.
                существительное:*{ !=n1:ОДУШ } }
  then -2
}

// Впрочем, необходимость в этом отпала.
//          ^^^^^^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:необходимость{}.предлог:в{}.*:*{ падеж:предл } }
  then 1
}


// Нет ни малейшей надежды на выздоровление
//                 ^^^^^^^^^^^^^^^^^^^^^^^^
tree_scorer ВалентностьСущ language=Russian
{
 if context { существительное:надежда{}.предлог:на{}.*:*{ падеж:вин } }
  then 2
}
