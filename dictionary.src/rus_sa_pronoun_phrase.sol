// ***********************
// МЕСТОИМЕНИЕ
// ***********************

// Правила для разбора прилагательного с постфиксами:
//
// Она же сама пришла!
// ^^^^^^^^^^^
// ты уж меня извини
// ^^^^^

patterns МестоимСПостфиксом export { ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }

pattern МестоимСПостфиксом
{
 местоимение:я{} : export { ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
}

wordentry_set ПостфиксМестоим=частица:{ же, ж, бы, б, ли, ль, уж }

// вас же опять возьмут под контроль
// ^^^^^^
// мне ли об этом говорить?
// ^^^^^^
// нас бы просто уничтожили
// ^^^^^^
pattern МестоимСПостфиксом
{
 n=местоимение:я{} : export { ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
 p=ПостфиксМестоим
} : links { n.<POSTFIX_PARTICLE>p }

// тебя-то
pattern МестоимСПостфиксом
{
 n=местоимение:я{} : export { ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
 t='-'
 p=частица:то{}
} : links { n.<POSTFIX_PARTICLE>t.<NEXT_COLLOCATION_ITEM>p }
: ngrams { 1 }


pattern МестоимСПостфиксом
{
 n=местоимение:я{} : export { ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
 t='-'
 p=частица:ка{}
} : links { n.<POSTFIX_PARTICLE>t.<NEXT_COLLOCATION_ITEM>p }
: ngrams { 1 }


// Он-де извинился
// ^^^^^
pattern МестоимСПостфиксом
{
 n=местоимение:я{} : export { ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
 t='-'
 p=частица:де{}
} : links { n.<POSTFIX_PARTICLE>t.<NEXT_COLLOCATION_ITEM>p }



// -----------------------------------------------------

patterns МестоимСПравПрил export { ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }

// Я же предупреждал вас
// ^^^^
pattern МестоимСПравПрил
{
 n=МестоимСПостфиксом : export { ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
}

// Я вижу его одного!
//        ^^^^^^^^^^
pattern МестоимСПравПрил
{
 n=МестоимСПостфиксом{ число:ед } : export { ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
 p=числительное:один{ =n:ПАДЕЖ }
}
: links { n.<ATTRIBUTE>p }


// здесь мы все трое обречены.
//       ^^^^^^^^^^^
pattern МестоимСПравПрил
{
 n=МестоимСПостфиксом{ ПАДЕЖ:ИМ ЧИСЛО:МН }: export { ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
 p=прилагательное:весь{ ПАДЕЖ:ИМ ЧИСЛО:МН }
 q=числительное:*{ ПАДЕЖ:ИМ КАТЕГОРИЯ_ЧИСЛ:СОБИР }
} : links { n.<ATTRIBUTE>q.<ATTRIBUTE>p }
  : ngrams { 2 }



// Специальные прилагательные-кванторы для правого или левого атрибута местоимения:
//
// мы оба кинулись к ней.
//    ^^^
//
// все вы правы
// ^^^^^^
patterns КванторДляМестоимЕд export { node:root_node РОД ПАДЕЖ ЧИСЛО ОДУШ }
patterns КванторДляМестоимМнож export { node:root_node РОД ПАДЕЖ ЧИСЛО ОДУШ }

wordentry_set КванторДляМестоимЕд0=прилагательное:{ весь, сам }

pattern КванторДляМестоимЕд
{
 ГруппаПрил1{ КванторДляМестоимЕд0 ЧИСЛО:ЕД } : export { node:root_node РОД ПАДЕЖ ЧИСЛО ОДУШ }
}

// Я и сам об этом не знал.
//   ^^^^^
pattern КванторДляМестоимЕд
{
 p=частица:и{}
 a=прилагательное:сам{ ЧИСЛО:ЕД } : export { node:root_node РОД ПАДЕЖ ЧИСЛО ОДУШ }
} : links { a.<PREFIX_PARTICLE>p }


wordentry_set КванторДляМестоимМн0=прилагательное:{ весь, сам, оба }

pattern КванторДляМестоимМнож
{
 ГруппаПрил1{ КванторДляМестоимМн0 ЧИСЛО:МН } : export { node:root_node РОД ПАДЕЖ ЧИСЛО ОДУШ }
}

pattern КванторДляМестоимМнож
{
 p=частица:и{}
 a=прилагательное:сам{ ЧИСЛО:МН } : export { node:root_node РОД ПАДЕЖ ЧИСЛО ОДУШ }
} : links { a.<PREFIX_PARTICLE>p }


// я вся горю
// ^^^^^
pattern МестоимСПравПрил
{
 p=МестоимСПостфиксом{ лицо:1 число:ед ~ПАДЕЖ:ВИН } : export{ ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
 adj=КванторДляМестоимЕд{ ~род:ср =P:ПАДЕЖ }
} : links { p.<ATTRIBUTE>adj }
  : ngrams { 1 }

// Сам я не справлюсь
// ^^^^^
pattern МестоимСПравПрил
{
 adj=КванторДляМестоимЕд{ ~род:ср ~ПАДЕЖ:ВИН }
 p=МестоимСПостфиксом{ лицо:1 число:ед =adj:ПАДЕЖ } : export{ ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
} : links { p.<ATTRIBUTE>adj }
  : ngrams { 1 }



// ты вся горишь
// ^^^^^^
pattern МестоимСПравПрил
{
 p=МестоимСПостфиксом{ лицо:2 число:ед ~ПАДЕЖ:ВИН } : export{ ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
 adj=КванторДляМестоимЕд{ ~род:ср =P:ПАДЕЖ }
} : links { p.<ATTRIBUTE>adj }
  : ngrams { 1 }


// Весь ты испачкался
// ^^^^^^^
pattern МестоимСПравПрил
{
 adj=КванторДляМестоимЕд{ ~род:ср ~ПАДЕЖ:ВИН }
 p=МестоимСПостфиксом{ лицо:2 число:ед =adj:ПАДЕЖ } : export{ ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
} : links { p.<ATTRIBUTE>adj }
  : ngrams { 1 }


// она вся горит
// ^^^^^^^
pattern МестоимСПравПрил
{
 p=МестоимСПостфиксом{ лицо:3 число:ед ~ПАДЕЖ:ВИН } : export{ ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
 adj=КванторДляМестоимЕд{ =P:род =P:ПАДЕЖ }
} : links { p.<ATTRIBUTE>adj }
  : ngrams { 1 }


// Само оно не рассосётся
// ^^^^^^^^
pattern МестоимСПравПрил
{
 adj=КванторДляМестоимЕд{ ~ПАДЕЖ:ВИН }
 p=МестоимСПостфиксом{ лицо:3 число:ед =adj:род =adj:ПАДЕЖ } : export{ ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
} : links { p.<ATTRIBUTE>adj }


// вы все сгорите!
// ^^^^^^
pattern МестоимСПравПрил
{
 p=МестоимСПостфиксом{ число:мн ~ПАДЕЖ:ВИН } : export{ ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
 adj=КванторДляМестоимМнож{ =P:ПАДЕЖ ~род:ср }
} : links { p.<ATTRIBUTE>adj }
  : ngrams { -1 }

// Все вы сдохните!
// ^^^^^^
pattern МестоимСПравПрил
{
 adj=КванторДляМестоимМнож{ ~ПАДЕЖ:ВИН ~род:ср }
 p=МестоимСПостфиксом{ число:мн =adj:ПАДЕЖ } : export{ ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
} : links { p.<ATTRIBUTE>adj }
  : ngrams { -1 }


// обе они оказались из серебра.
// ^^^^^^^
pattern МестоимСПравПрил
{
 adj=КванторДляМестоимМнож{ ~ПАДЕЖ:ВИН ~род:ср }
 p=МестоимСПостфиксом{ число:мн =adj:ПАДЕЖ } : export{ ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
} : links { p.<ATTRIBUTE>adj }



// --------------------------------------------------
// отдельно для винительного падежа, чтобы
// правильно связывать: "тебя же весь город знает!"
// --------------------------------------------------

// Они облили меня всего
//            ^^^^^^^^^^
pattern МестоимСПравПрил
{
 p=МестоимСПостфиксом{ лицо:1 число:ед ПАДЕЖ:ВИН } : export{ ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
 adj=КванторДляМестоимЕд{ ~род:ср ПАДЕЖ:ВИН ОДУШ:ОДУШ }
} : links { p.<ATTRIBUTE>adj }
  : ngrams { 1 }

// Всего меня облили зеленкой.
// ^^^^^^^^^^
pattern МестоимСПравПрил
{
 adj=КванторДляМестоимЕд{ ~род:ср ПАДЕЖ:ВИН ОДУШ:ОДУШ }
 p=МестоимСПостфиксом{ лицо:1 число:ед ПАДЕЖ:ВИН } : export{ ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
} : links { p.<ATTRIBUTE>adj }
  : ngrams { 1 }


// Я вижу тебя всего
//        ^^^^^^^^^^
pattern МестоимСПравПрил
{
 p=МестоимСПостфиксом{ лицо:2 число:ед ПАДЕЖ:ВИН } : export{ ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
 adj=КванторДляМестоимЕд{ ~род:ср ПАДЕЖ:ВИН ОДУШ:ОДУШ }
} : links { p.<ATTRIBUTE>adj }
  : ngrams { 1 }

// Я вижу всего тебя насквозь
//        ^^^^^^^^^^
pattern МестоимСПравПрил
{
 adj=КванторДляМестоимЕд{ ~род:ср ПАДЕЖ:ВИН ОДУШ:ОДУШ }
 p=МестоимСПостфиксом{ лицо:2 число:ед ПАДЕЖ:ВИН } : export{ ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
} : links { p.<ATTRIBUTE>adj }



// Я вижу её всю
//        ^^^^^^
pattern МестоимСПравПрил
{
 p=МестоимСПостфиксом{ лицо:3 число:ед ПАДЕЖ:ВИН род:жен } : export{ ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
 adj=КванторДляМестоимЕд{ =P:род ПАДЕЖ:ВИН }
} : links { p.<ATTRIBUTE>adj }
  : ngrams { 1 }

// Я вижу всю её.
//        ^^^^^^
pattern МестоимСПравПрил
{
 adj=КванторДляМестоимЕд{ ПАДЕЖ:ВИН РОД:ЖЕН }
 p=МестоимСПостфиксом{ лицо:3 число:ед ПАДЕЖ:ВИН род:жен } : export{ ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
} : links { p.<ATTRIBUTE>adj }


// Я вижу его всего
//        ^^^^^^^^^
pattern МестоимСПравПрил
{
 p=МестоимСПостфиксом{ лицо:3 число:ед ПАДЕЖ:ВИН род:муж } : export{ ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
 adj=КванторДляМестоимЕд{ =P:род ПАДЕЖ:ВИН ОДУШ:ОДУШ }
} : links { p.<ATTRIBUTE>adj }
  : ngrams { 1 }

// Я вижу всего его.
//        ^^^^^^^^^
pattern МестоимСПравПрил
{
 adj=КванторДляМестоимЕд{ род:муж ПАДЕЖ:ВИН ОДУШ:ОДУШ }
 p=МестоимСПостфиксом{ лицо:3 число:ед ПАДЕЖ:ВИН род:муж } : export{ ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
} : links { p.<ATTRIBUTE>adj }



// я вижу вас всех!
//        ^^^^^^^^
pattern МестоимСПравПрил
{
 p=МестоимСПостфиксом{ число:мн ПАДЕЖ:ВИН } : export{ ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
 adj=КванторДляМестоимМнож{ ПАДЕЖ:ВИН ОДУШ:ОДУШ }
} : links { p.<ATTRIBUTE>adj }
  : ngrams { -1 }

// Мы всех вас выведем на чистую воду.
//    ^^^^^^^^
pattern МестоимСПравПрил
{
 adj=КванторДляМестоимМнож{ ПАДЕЖ:ВИН ОДУШ:ОДУШ }
 p=МестоимСПостфиксом{ число:мн ПАДЕЖ:ВИН } : export{ ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
} : links { p.<ATTRIBUTE>adj }
  : ngrams { -1 }


// можем попытаться отыскать их всех до одного.
//                           ^^^^^^^^^^^^^^^^^
pattern МестоимСПравПрил
{
 p=МестоимСПостфиксом{ число:мн ПАДЕЖ:ВИН } : export{ ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
 adj=прилагательное:весь{ ЧИСЛО:МН ПАДЕЖ:ВИН ОДУШ:ОДУШ }
 prep=предлог:до{}
 adj2=прилагательное:*{ ПАДЕЖ:РОД ЧИСЛО:ЕД РОД:МУЖ }
} : links { p.<ATTRIBUTE>adj.<PREPOS_ADJUNCT>prep.<OBJECT>adj2 }
  : ngrams { -1 }


// но ему нельзя видеть нас двоих вместе!
//                      ^^^^^^^^^
pattern МестоимСПравПрил
{
 n=МестоимСПостфиксом{ число:мн } : export { ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
 p=числительное:*{ =n:ПАДЕЖ }
}
: links { n.<ATTRIBUTE>p }


// Ты, высокий, подойди сюда быстро!
// ^^^^^^^^^^^^
pattern МестоимСПравПрил
{
 n=МестоимСПостфиксом : export { ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
 comma1=','
 a=ГруппаПрил2{ =n:ЧИСЛО =n:ПАДЕЖ =n:РОД }
 comma2=@coalesce(',')
} : links
{
 n.<ATTRIBUTE>a.{
                 <PUNCTUATION>comma1
                 ~<PUNCTUATION>comma2
                }
}





// -----------------------------------------------------

// Правила для сборки местоимения с префиксами и постфиксами:
//
// И не вам бы говорить это!
//   ^^^^^^^^^
patterns МестоимСПрефиксом export { ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }


pattern МестоимСПрефиксом
{
 МестоимСПравПрил : export { ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
}

// Ты должен хранить верность не нам.
//                            ^^^^^^
pattern МестоимСПрефиксом
{
 p=частица:не{}
 n=МестоимСПрефиксом : export { ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
} : links { n.<NEGATION_PARTICLE>p }


// даже мне это известно
// ^^^^^^^^
pattern МестоимСПрефиксом
{
 p=частица:даже{}
 n=МестоимСПрефиксом : export { ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
} : links { n.<PREFIX_PARTICLE>p }
  : ngrams { -2 }

// вот тебя там ждут
// ^^^^^^^^
pattern МестоимСПрефиксом
{
 p=частица:вот{}
 n=МестоимСПрефиксом : export { ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
} : links { n.<PREFIX_PARTICLE>p }

// Поймете и вы.
//         ^^^^
pattern МестоимСПрефиксом
{
 p=частица:и{}
 n=МестоимСПрефиксом : export { ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
}
: links { n.<PREFIX_PARTICLE>p }
: ngrams { -5 }

// - Ишь ты, заговорил!
//   ^^^^^^
pattern МестоимСПрефиксом
{
 p=частица:*{}
 n=местоимение:*{} : export { ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
}
: links { n.<PREFIX_PARTICLE>p }
: ngrams { -6 }



// -----------------------------------------------------

patterns МестоимЯдро export { ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }

// тебя
pattern МестоимЯдро
{
 МестоимСПрефиксом : export { ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
}

wordentry_set НаречДляМест=наречие:{ лично, персонально }

// этот результат нужен мне лично
//                      ^^^^^^^^^
pattern МестоимЯдро
{
 p=МестоимСПрефиксом : export { ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
 adv=НаречДляМест
} : links { p.<ATTRIBUTE>adv }


// этот результат нужен персонально мне
//                      ^^^^^^^^^^^^^^^
pattern МестоимЯдро
{
 adv=НаречДляМест
 p=МестоимЯдро : export { ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
} : links { p.<ATTRIBUTE>adv }


// лететь должны именно вы
//               ^^^^^^^^^
pattern МестоимЯдро
{
 mod=УсилитНаречие
 p=МестоимЯдро : export { ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
}
: links { p.<ATTRIBUTE>mod }
//: ngrams { -1 }

// -------------------------------------------------

pattern ГруппаМест
{
 p=МестоимЯдро : export { ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
}


// тебя и нас
pattern ГруппаМест
{
 p=МестоимЯдро : export { ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
 conj=союз:и{}
 p2=МестоимЯдро{ =P:ПАДЕЖ }
} : links { p.<RIGHT_LOGIC_ITEM>conj.<NEXT_COLLOCATION_ITEM>p2 }


// Ты, и только ты способен на это.
// ^^^^^^^^^^^^^^^
pattern ГруппаМест
{
 p=МестоимЯдро : export { ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
 comma=','
 conj=союз:и{}
 p2=МестоимЯдро{ =P:ПАДЕЖ }
} : links { p.<RIGHT_LOGIC_ITEM>comma.<NEXT_COLLOCATION_ITEM>conj.<NEXT_COLLOCATION_ITEM>p2 }



// тебя с ней
pattern ГруппаМест
{
 p=МестоимЯдро : export { ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
 conj=предлог:с{}
 p2=МестоимЯдро{ ПАДЕЖ:ТВОР }
} : links { p.<RIGHT_LOGIC_ITEM>conj.<NEXT_COLLOCATION_ITEM>p2 }


// я тебя, дурака, пальцем тронула?
//   ^^^^^^^^^^^^^
pattern ГруппаМест
{
 p=МестоимЯдро : export{ ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }
 comma1=','
 det=ГруппаСущ4{ =p:ЧИСЛО =p:ПАДЕЖ }
 @noshift(ПравыйОграничительОборота)
 comma2=@coalesce(',')
}
: links
{
 p.{
    <PUNCTUATION>comma1
    <DETAILS>det
    ~<PUNCTUATION>comma2
   }
}
: ngrams { -5 }


// Я и Саша пойдём в кино.
pattern ГруппаМест
{
 p=МестоимЯдро : export{ ПАДЕЖ ЧИСЛО:МН РОД ЛИЦО node:root_node }
 conj=союз:и{}
 n2=ГлДополнение{ =p:ПАДЕЖ }
} : links
{
 p.<RIGHT_LOGIC_ITEM>conj.<NEXT_COLLOCATION_ITEM>n2
}


// неуемная натура его осложняет  жизнь  и ему, и мне.
//                                       ^^^^^^^^^^^^
pattern ГруппаМест
{
 conj1=СочинительныйСоюз1
 p=МестоимЯдро : export{ ПАДЕЖ ЧИСЛО:МН РОД ЛИЦО node:root_node }
 comma=','
 conj2=СочинительныйСоюз2
 n2=ГлДополнение{ =p:ПАДЕЖ }
} : links
{
 p.{
    <PREFIX_CONJUNCTION>conj1
    <RIGHT_LOGIC_ITEM>comma.
     <NEXT_COLLOCATION_ITEM>conj2.
      <NEXT_COLLOCATION_ITEM>n2
   }
}

pattern ГруппаМест
{
 conj1=ЛогичСоюз
 p=МестоимЯдро : export { ПАДЕЖ ЧИСЛО:МН РОД ЛИЦО node:root_node }
 comma=','
 conj2=ЛогичСоюз
 n2=ГлДополнение{ =p:ПАДЕЖ }
} : links
{
 p.{
    <PREFIX_CONJUNCTION>conj1
    <RIGHT_LOGIC_ITEM>comma.
     <NEXT_COLLOCATION_ITEM>conj2.
      <NEXT_COLLOCATION_ITEM>n2
   }
}


// Немного упрощенный подход к сборке такой конструкции,
// но это не должно мешать ничему:
// - Ты  что,  смеешься?
//   ^^^^^^^
// Тебе что, плохо живется?
// ^^^^^^^^^
pattern ГруппаМест
{
 n=МестоимЯдро : export { ПАДЕЖ ЧИСЛО:МН РОД ЛИЦО node:root_node }
 p=местоим_сущ:что{ падеж:им }
 p2=@optional(частица:же{})
 comma=','
}
: links { n.<POSTFIX_PARTICLE>p.{
                                 ~<POSTFIX_PARTICLE>p2
                                 <PUNCTUATION>comma
                                }
        }
: ngrams { 1 }


